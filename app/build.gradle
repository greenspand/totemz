apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 3

version "${versionMajor}.${versionMinor}.${versionPatch}"

def buildNumberProduction = 4
def buildNumberDebugging = 999999

def buildVersionCode = { ->
    if (gradle.startParameter.taskNames.contains(":app:assembleRelease")) {
        return buildNumberProduction
    } else {
        return buildNumberDebugging
    }
}


android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"

    /**CONFIGURE SPLITS*/
    splits {
        abi {
            /*Enable splits only if we perform the build production release task*/
            enable gradle.startParameter.taskNames.contains(":app:assembleRelease")
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', "x86", 'x86_64', "mips", 'mips64'
            /*No need for universal apk*/
            universalApk false
        }
    }

    // map for the version code
    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 4, 'x86': 5, 'x86_64': 6, 'mips64': 7]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) + buildNumberProduction
        }
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        applicationId "ro.cluj.totemz"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode buildVersionCode()

        versionName "${version}"
        vectorDrawables.useSupportLibrary = true
        /** Keep language resources in libraries only for supported locales*/
        resConfigs "en", "de", "ro"
    }

    buildTypes {
        debug {
            multiDexEnabled true
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    productFlavors {
    }

    lintOptions {
        abortOnError false
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$supportlibVersion"
    compile "com.android.support:animated-vector-drawable:$supportlibVersion"
    compile "com.android.support:cardview-v7:$supportlibVersion"
    compile "com.android.support:customtabs:$supportlibVersion"
    compile "com.android.support:recyclerview-v7:$supportlibVersion"
    compile "com.android.support:design:$supportlibVersion"

    /**MultiDex*/
    compile 'com.android.support:multidex:1.0.1'

    compile "com.google.android.gms:play-services-maps:$playServicesVersion"
    compile "com.google.android.gms:play-services-ads:$playServicesVersion"
    compile "com.google.android.gms:play-services-auth-base:$playServicesVersion"
    compile "com.google.android.gms:play-services-location:$playServicesVersion"

    /**Map Utils*/
    compile 'com.google.maps.android:android-maps-utils:0.4+'

    /**Kotlin*/
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    /**Coroutines**/
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.15'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.15'

    /**Firebase Core*/
    compile "com.google.firebase:firebase-core:$playServicesVersion"

    /**Firebase DB*/
    compile "com.google.firebase:firebase-database:$playServicesVersion"

    /**Firebase Geofire*/
    compile 'com.firebase:geofire-android:2.1.1'

    /**Firebase Push Notifications*/
    compile "com.google.firebase:firebase-messaging:$playServicesVersion"

    /**Firebase crashlitics*/
    compile "com.google.firebase:firebase-crash:$playServicesVersion"


    /**Firebase authentication*/
    compile "com.google.firebase:firebase-auth:$playServicesVersion"
    compile "com.google.android.gms:play-services-auth:$playServicesVersion"

    /**Kotlin extensions*/
    compile "com.greenspand:kotlin-ext:$ktExtVersion"

    /**Permissions*/
    compile 'com.karumi:dexter:4.1.0'

    /**Logger*/
    compile 'com.jakewharton.timber:timber:4.5.1'

    /**Img loader*/
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'jp.wasabeef:picasso-transformations:2.1.2'

    /**Protocol Buffers*/
    compile 'com.squareup.wire:wire-runtime:2.2.0'

    /**MQTT*/
    compile "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$mqttVersion"
    compile ("org.eclipse.paho:org.eclipse.paho.android.service:$mqttVersionSnapshot@aar"){
        exclude module: 'support-v4'
    }


    /**DI Framework*/
    compile "com.github.salomonbrys.kodein:kodein:$kodeinVersion"
    compile "com.github.salomonbrys.kodein:kodein-android:$kodeinVersion"

    /**Memory leaks detector*/
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Android SDK
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
    // Audience Network SDK. Only versions 4.6.0 and above are available
    compile 'com.facebook.android:audience-network-sdk:4.+'

    /**Rx Java*/
    compile "io.reactivex.rxjava2:rxandroid:$rxVersion"
    compile "io.reactivex.rxjava2:rxjava:$rxVersion"

    /**Fabric IO crashlytics*/
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }

    /**Twitter authentication*/
    compile('com.twitter.sdk.android:twitter:2.3.2@aar') {
        transitive = true;
    }

    /**Pulse animation*/
    compile 'pl.bclogic:pulsator4droid:1.0.3'
}
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.gms.google-services'
